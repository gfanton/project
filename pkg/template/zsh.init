# compdef p
# mostly from: https://github.com/ajeetdsouza/zoxide/blob/main/templates/zsh.txt

function __project_pwd() {
    \builtin pwd -L
}

function __project_cd() {
    \builtin cd -- "$@" && echo "switched to '$@'"
}

function __project_p() {
    # shellcheck disable=SC2199
    if [[ "$#" -eq 0 ]]; then
        __project_cd ~
    elif [[ "$#" -eq 1 ]] && [[ "$1" = '-' ]]; then
        if [[ -n "${OLDPWD}" ]]; then
            __project_cd "${OLDPWD}"
        else
            # shellcheck disable=SC2016
            \builtin printf 'project: $OLDPWD is not set'
            return 1
        fi
    elif [[ "$#" -eq 1 ]] && [[ -d "$1" ]]; then
        __project_cd "$1"
    # elif [[ "$@[-1]" == "${__project_z_prefix}"* ]]; then
    #     # shellcheck disable=SC2124
    #     \builtin local result="${@[-1]}"
    #     __project_cd "{{ "${result:${#__project_z_prefix}}" }}"
    else
        \builtin local result
        # shellcheck disable=SC2312
        result="$(\command {{.Exec}} query --abspath --exclude "$(__project_pwd)" -- "$@")" &&
            __project_cd "${result}"
    fi
}

# Completions.
# Check if the Zsh Line Editor (zle) is available. 
# If it's not, none of the rest of the code within this if statement block will execute.
if [[ -o zle ]]; then
    # Define completion function with menu behavior override
    function __project_p_complete() {
        [[ "{{ "${#words[@]}" }}" -eq "${CURRENT}" ]] || return 0

        \builtin local typed="${words[2,-1]}"
        \builtin local IFS=$'\n'
        \builtin local -a matches
        matches=($(\command {{.Exec}} query --limit 20 --exclude "$(__project_pwd)" -- ${typed} 2>/dev/null))
        
        if [[ ${#matches[@]} -gt 0 ]]; then
            # Enable menu complete for this session
            \builtin local old_menu_complete
            [[ -o MENU_COMPLETE ]] && old_menu_complete=1 || old_menu_complete=0
            \builtin setopt MENU_COMPLETE
            
            _wanted projects expl 'project' compadd "${matches[@]}"
            
            # Restore previous setting
            [[ $old_menu_complete -eq 0 ]] && \builtin unsetopt MENU_COMPLETE
        fi

        return 0
    }

    # Register completion function
    [[ "${+functions[compdef]}" -ne 0 ]] && \compdef __project_p_complete p
fi

\builtin unalias p &>/dev/null || \builtin true
\builtin alias p=__project_p

# To initialize project, add this to your configuration (usually ~/.zshrc):
#
# eval "$(proj init zsh)"
